import au.id.micolous.metrodroid.LocalizeGenerator

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }
}

buildscript {
    ext {
        kotlin_version = '1.3.41'
        kotlinio_version = '0.1.9-1.3.40-eap-67'
        kotlinserial_version = '0.11.1-1.3.40-eap-107'
        coroutines_version = '1.2.2'
        lint_version = '26.2.1'
        support_version = '28.0.0'
        android_test_version = '1.2.0'
        build_version = '29.0.0'
        android_sdk_version = 29
        android_min_version = 16
    }
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.6'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-multiplatform'
apply plugin: 'checkstyle'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.getkeepsafe.dexcount'

def generatedLocalizeDir = new File(buildDir, "generated/source/localize")
def generatedLanguagesDir = new File(buildDir, "generated/res/localize/iOS")
def drawableDir = new File(projectDir, "src/main/res/drawable")
def drawableHdpiDir = new File(projectDir, "src/main/res/drawable-hdpi")
def stringsXmlFile = new File(projectDir, "src/main/res/values/strings.xml")

task generateLocalize {
    inputs.file stringsXmlFile
    inputs.dir drawableDir
    inputs.dir drawableHdpiDir
    outputs.dir generatedLocalizeDir
    doLast {
        LocalizeGenerator.INSTANCE.generateLocalize(generatedLocalizeDir, stringsXmlFile, [drawableDir, drawableHdpiDir])
    }
}

preBuild.dependsOn generateLocalize

/*
check.dependsOn 'checkstyle'

task checkstyle(type: Checkstyle) {
    source 'src'
    include '** /*.java'
    exclude '** /gen/**'
    classpath = files()
}
*/

dependencies {
    implementation("androidx.appcompat:appcompat:1.0.0")
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.annotation:annotation:1.0.0"
    implementation "androidx.preference:preference:1.0.0"
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
    implementation project(':leaflet')
    implementation project(':material-design-icons')
    implementation project(':proto')

    implementation 'com.github.bmelnychuk:atv:1.2.9'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    androidTestUtil "androidx.test:orchestrator:$android_test_version"
    androidTestImplementation "androidx.test:rules:$android_test_version"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    testImplementation 'org.robolectric:robolectric:4.3'

    lintChecks project(':lintchecks')
}

task copyThirdPartyNotices(type: Copy) {
    from 'third_party/'
    into 'build/assets/third_party'
    eachFile { it.path = name }
    include 'NOTICE.**.txt'
}

kotlin {
    targets {
        fromPreset(presets.android, 'android')
        fromPreset(presets.jvm, 'jvmCli') {
            compilations.all {
                tasks[compileKotlinTaskName].dependsOn += generateLocalize
            }
        }

        final def iOSTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos") \
                              ? presets.iosArm64 : presets.iosX64

        fromPreset(iOSTarget, 'iOS') {
            binaries {
                framework("metrolib") {
                    linkTask.dependsOn += 'generateLocalize'
                    linkTask.dependsOn += 'iOSLanguages'
                }
            }
            binaries.all {
                linkerOpts new File(project('proto').buildDir, 'libs/main/static/libmain.a').path
            }

            compilations.all {
                tasks[compileKotlinTaskName].dependsOn += 'generateLocalize'
                tasks[compileKotlinTaskName].dependsOn += 'iOSLanguages'
                cinterops {
                          stations {
                                   defFile project.file("src/iOSMain/cinterop/stations.def")
                                   includeDirs(new File(project('proto').buildDir, 'generated/source/proto/main/objc'))
                                   compilerOpts '-I' + projectDir + '/third_party/protobuf/objectivec'
                          }
                }
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                api "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
                api "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$kotlinserial_version"
                api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                api "org.jetbrains.kotlinx:kotlinx-io:$kotlinio_version"
            }

            kotlin.srcDirs += new File(generatedLocalizeDir, 'commonMain/kotlin')
        }

        commonTest {
            dependencies {
                api "org.jetbrains.kotlin:kotlin-test-common:$kotlin_version"
                api "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlin_version"
                api "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
                api "org.jetbrains.kotlinx:kotlinx-io:$kotlinio_version"
            }
        }

        androidMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-io-jvm:$kotlinio_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlinserial_version"
            }

            kotlin.srcDirs += new File(generatedLocalizeDir, 'androidMain/kotlin')
            kotlin.srcDirs += 'src/jvmCommonMain/kotlin'
        }

        iOSMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-io-native:$kotlinio_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:$kotlinserial_version"
                implementation project(':proto')
            }

            kotlin.srcDirs += new File(generatedLocalizeDir, 'iOSMain/kotlin')
        }

        iOSTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-io-native:$kotlinio_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:$kotlinserial_version"
                implementation project(':proto')
            } 
        }

        androidTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlinserial_version"
                implementation "org.jetbrains.kotlinx:kotlinx-io-jvm:$kotlinio_version"
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
                implementation project(':proto')
            }
            kotlin.srcDirs += [
                'src/jvmCommonTest/kotlin'
            ]
        }


        jvmCliTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }

            resources.srcDirs += [
                    'src/commonTest/assets',
                    'third_party/vpereira_bilhete/',
                    'third_party/RejsekortReader/dumps/'
            ]
            kotlin.srcDirs += [
                    'src/jvmCommonTest/kotlin'
            ]
        }

        jvmCliMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlinserial_version"
                implementation 'commons-io:commons-io:2.5'
                implementation 'org.apache.commons:commons-lang3:3.5'
                implementation "org.jetbrains.kotlinx:kotlinx-io-jvm:$kotlinio_version"
                implementation 'com.google.protobuf:protobuf-lite:3.0.1'
                implementation 'com.github.ajalt:clikt:2.1.0'
                implementation group: 'xmlpull', name: 'xmlpull', version: '1.1.3.4a'
                implementation project(':proto')
            }
            kotlin.srcDirs += new File(generatedLocalizeDir, 'jvmCliMain/kotlin')
            resources.srcDirs += new File("$projectDir/mdst")
            kotlin.srcDirs += 'src/jvmCommonMain/kotlin'
        }
    }
}

def commonLanguageList = [
                // Update this whenever new language translations become available in Weblate.
                // Languages we support
                // Also add the language in XCode
                'en',
                'de',
                'el',
                'es',
                'fi',
                'fr',
                'he',
                'id',
                'it',
                'ja',
                'kab',
                'nb-rNO',
                'nl',
                'pt-rBR',
                'ru',
                'tr',
                'zh-rCN'
                ] 

def iOSLanguageList = commonLanguageList

def androidLanguageList = iOSLanguageList + [
                // Regions for English. We currently omit those on iOS
                'en-rAU', 'en-rCA', 'en-rGB', 'en-rIE', 'en-rIN',  'en-rNZ', 'en-rUS',
                // We omit languages without real translations on iOS
                'ga',
                'mi',
                'ms',
                'zh-rHK',
                'zh-rSG',
                'zh-rTW',

                // obsolete identifier
                'iw',
                'in'
                ]

def mapAndroidLanguageToIOS(String alang) {
    if (alang == "nb-rNO")
        return "nb"
    if (alang == "zh-rCN")
        return "zh-Hans"
    return alang.replaceAll(/^([a-z]*)-r([A-Z]*)$/) { all, lang, region -> "$lang-$region" }
}

task iOSLanguages {
    inputs.file stringsXmlFile
    outputs.dir generatedLanguagesDir
    doLast {
        for (lang in iOSLanguageList) {
            LocalizeGenerator.INSTANCE.generateAppleStringsFile(
              new File(generatedLanguagesDir, mapAndroidLanguageToIOS(lang) + '.lproj/Metrolib.strings'),
              new File(projectDir, "src/main/res/values-" + lang + "/strings.xml"),
              lang
            )
        }
    }
}

task iOSMappedLanguages {
    doLast {
        for (lang in iOSLanguageList) {
            LocalizeGenerator.INSTANCE.generateMappedAppleStringsFile(
              new File(projectDir, 'native/metrodroid/metrodroid/' + mapAndroidLanguageToIOS(lang) + '.lproj/Main.strings'),
              new File(projectDir, "src/main/res/values-" + lang + "/strings.xml"),
              new File(projectDir, 'native/metrodroid/metrodroid/MainMap.txt')
            )
            LocalizeGenerator.INSTANCE.generateMappedAppleStringsFile(
              new File(projectDir, 'native/metrodroid/metrodroid/metrodroid/' + mapAndroidLanguageToIOS(lang) + '.lproj/InfoPlist.strings'),
              new File(projectDir, "src/main/res/values-" + lang + "/strings.xml"),
              new File(projectDir, 'native/metrodroid/metrodroid/InfoPlistMap.txt')
            )
            LocalizeGenerator.INSTANCE.generateMappedAppleStringsFile(
              new File(projectDir, 'native/metrodroid/Settings.bundle/' + mapAndroidLanguageToIOS(lang) + '.lproj/Root.strings'),
              new File(projectDir, "src/main/res/values-" + lang + "/strings.xml"),
              new File(projectDir, 'native/metrodroid/SettingsMap.txt')
            )
        }
    }
}

task packForXCode(type: Sync) {
    final File frameworkDir = new File(buildDir, "xcode-frameworks")
    final String mode = project.findProperty("XCODE_CONFIGURATION")?.toUpperCase() ?: 'DEBUG'
    final def framework = kotlin.targets.iOS.binaries.getFramework("metrolib", mode)

    inputs.property "mode", mode
    dependsOn framework.linkTask
    dependsOn iOSLanguages

    from framework.outputFile
    into new File (frameworkDir, "metrolib.framework")

    from generatedLanguagesDir
    into new File (frameworkDir, "metrolib.framework")
}

tasks.build.dependsOn packForXCode

task iOSTest {
    def device = project.findProperty("iosDevice")?.toString() ?: "iPhone 8"
    dependsOn 'linkDebugTestIOS'
    group = JavaBasePlugin.VERIFICATION_GROUP
    description = "Runs tests for target 'ios' on an iOS simulator"

    doLast {
        def binary = kotlin.targets.iOS.binaries.getTest('DEBUG').outputFile
        exec {
            commandLine 'xcrun', 'simctl', 'spawn', device, binary.absolutePath
        }
    }
}

android {
    compileSdkVersion android_sdk_version
    buildToolsVersion build_version

    defaultConfig {
        minSdkVersion android_min_version
        targetSdkVersion android_sdk_version
        testApplicationId "au.id.micolous.farebot.test"
        versionCode "git -C ${projectDir} rev-list --count HEAD".execute().text.trim().toInteger()
        versionName "git -C ${projectDir} describe --always --tags --dirty".execute().text.trim().replaceAll("^v", "")

        resConfigs (androidLanguageList)
        vectorDrawables {
            useSupportLibrary = true
            generatedDensities 'ldpi', 'mdpi', 'hdpi'
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    buildTypes {
        debug {
            pseudoLocalesEnabled true
        }
        dev.initWith(buildTypes.debug)
        dev {
            applicationIdSuffix '.dev'
            minifyEnabled true
            shrinkResources true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard.cfg'
            matchingFallbacks = ['debug']
        }
        release {
            minifyEnabled true
            shrinkResources true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard.cfg'
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == "debug") {
            variant.mergedFlavor.resourceConfigurations.add("en-rXA")
            variant.mergedFlavor.resourceConfigurations.add("ar-rXB")
        }
    }

    preBuild.dependsOn copyThirdPartyNotices

    sourceSets {
        androidTest.assets.srcDirs += [
                'src/commonTest/assets',
                'third_party/vpereira_bilhete/',
                'third_party/RejsekortReader/dumps/'
        ]
        main.assets.srcDirs += ['build/assets', 'mdst']
        test.kotlin.srcDirs += ['src/androidUnitTest/kotlin']
        test.resources.srcDirs += [
                    'src/commonTest/assets',
                    'third_party/vpereira_bilhete/',
                    'third_party/RejsekortReader/dumps/'
        ]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/kotlinx-io.kotlin_module'
        exclude 'META-INF/atomicfu.kotlin_module'
        exclude 'META-INF/main.kotlin_module'
        exclude 'META-INF/kotlinx-serialization-runtime.kotlin_module'
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    // Long timeouts needed for slow environments (like Travis CI)
    adbOptions {
        timeOutInMs 10 * 60 * 1000 // 10 minutes
    }
}

kotlin.sourceSets.all {
    languageSettings {
        useExperimentalAnnotation('kotlin.ExperimentalUnsignedTypes')
    }
}

androidExtensions {
    // Needed for kotlinx.android.parcel.Parcelize
    experimental = true
}
repositories {
    mavenCentral()
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

// Activate reproducible archives
tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}
